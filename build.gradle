apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'codenarc'
apply plugin: 'findbugs'

mainClassName = 'com.tngtech.jenkins.notification.Bootstrap'
sourceCompatibility = '1.7'

//buildscript {
//    repositories {
//        mavenCentral()
//    }
//
//    dependencies {
//        classpath('au.com.ish.gradle:release:2.2.2') {
//            exclude group: 'org.tmatesoft.svnkit'
//        }
//    }
//}
//
//apply plugin: 'release'
//
//release {
//    failOnSnapshotDependencies = true
//    allowLocalModifications = true
//    releaseDryRun = false
//    scm = 'git'
//}
//version = release.projectVersion
version='0.9.9-SNAPSHOT'

codenarc {
    configFile = file('config/codenarc/rules.groovy')
}

codenarcTest {
    configFile = file('config/codenarc/testRules.groovy')
}

findbugs {
    sourceSets = [sourceSets.main]
    excludeFilter = file('config/findbugs/exclude.xml')
}

repositories {
    mavenCentral()
    maven {
        url 'http://nexus.ailis.de/content/groups/public'
    }
}
ext {
    camelVersion = '2.13.0'
    groovyVersion = '2.3.2'
}

tasks.withType(AbstractCompile) {
    options.encoding = 'UTF-8'
}

configurations {
    all*.exclude group: 'org.codehaus.groovy', module: 'groovy-all'
}

dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy', version: groovyVersion
    compile group: 'org.codehaus.groovy', name: 'groovy-json', version: groovyVersion
    compile group: 'org.codehaus.groovy', name: 'groovy-xml', version: groovyVersion
    compile group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.1'
    compile group: 'de.ailis.usb4java', version: '1.0.0', name: 'usb4java'
    compile group: 'org.apache.camel', name: 'camel-core', version: camelVersion
    compile group: 'org.apache.camel', name: 'camel-atom', version: camelVersion
    compile group: 'javazoom', name: 'jlayer', version: '1.0.1'
    runtime group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.6'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '0.7-groovy-2.0'
    testCompile group: 'cglib', name: 'cglib-nodep', version: '3.1'
    testCompile group: 'org.apache.camel', name: 'camel-test', version: camelVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '1.5.0'

    codenarc group:'org.codenarc', name: 'CodeNarc', version: '0.24'

    findbugs group: 'com.google.code.findbugs', name: 'findbugs', version: '2.0.3'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task jpi(dependsOn: distZip)

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.tngtech.jenkins.notification.retaliation'
            artifactId = 'jenkins-rss-retaliation'
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            artifact distZip {
                classifier "dist"
            }
        }
    }
    repositories {
        maven {
            url 'https://api.bintray.com/maven/wolfs/maven/jenkins-rss-retaliation'
            credentials {
                username = 'wolfs'
                password = hasProperty('bintrayApiKey') ? bintrayApiKey : ''
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
